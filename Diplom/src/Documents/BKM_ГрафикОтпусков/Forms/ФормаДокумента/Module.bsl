#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковПриИзменении(Элемент)
	
	ОтпускаСотрудниковПриИзмененииНаСервере();
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ЗаполнитьСотрудниками(Команда) 
	
	Если Объект.ОтпускаСотрудников.Количество() <> 0  Тогда
		
		Результат = Ждать ВопросАсинх("Список сотрудников будет очищен. Продолжить?", РежимДиалогаВопрос.ДаНет,,,"Вопрос.");	
		
		Если Результат = Неопределено Или Результат = КодВозвратаДиалога.Нет Тогда				
			Возврат;			
		КонецЕсли; 
		
	КонецЕсли;
	
	Объект.ОтпускаСотрудников.Очистить();
	ЗаполнитьСотрудникамиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура АнализГрафика(Команда)
	
	ПараметрыФормы = Новый Структура;	
	ПараметрыФормы.Вставить("Адрес", ПоместитьДанныеВоВременноеХранилище()); 
	
	ОткрытьФорму("Документ.BKM_ГрафикОтпусков.Форма.ФормаАнализГрафика", ПараметрыФормы, ЭтотОбъект);   	
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтпускаСотрудниковПриИзмененииНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СписокОтпусков.Сотрудник КАК Сотрудник,
		|	СписокОтпусков.НомерСтроки КАК НомерСтроки,
		|	РАЗНОСТЬДАТ(СписокОтпусков.ДатаНачала, СписокОтпусков.ДатаОкончания, ДЕНЬ) + 1 КАК ОбщаяПродолжительностьОтпуска
		|ПОМЕСТИТЬ ВТ_СписоОтпусков
		|ИЗ
		|	&СписокОтпусков КАК СписокОтпусков
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СписоОтпусков.Сотрудник КАК Сотрудник,
		|	СУММА(ВТ_СписоОтпусков.ОбщаяПродолжительностьОтпуска) КАК ОбщаяПродолжительностьОтпуска
		|ПОМЕСТИТЬ ВТ_СписокСгруппированный
		|ИЗ
		|	ВТ_СписоОтпусков КАК ВТ_СписоОтпусков
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_СписоОтпусков.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_СписоОтпусков.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ВТ_СписокСгруппированный.ОбщаяПродолжительностьОтпуска, 0) КАК ОбщаяПродолжительностьОтпуска
		|ИЗ
		|	ВТ_СписоОтпусков КАК ВТ_СписоОтпусков
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокСгруппированный КАК ВТ_СписокСгруппированный
		|		ПО ВТ_СписоОтпусков.Сотрудник = ВТ_СписокСгруппированный.Сотрудник";
	
	
	СписокОтпусков = Объект.ОтпускаСотрудников.Выгрузить(,"Сотрудник,НомерСтроки,ДатаНачала,ДатаОкончания" );
	Запрос.УстановитьПараметр("СписокОтпусков",СписокОтпусков);
	
	ТаблицаОтпусков = Запрос.Выполнить().Выбрать();   
	
		
	Пока ТаблицаОтпусков.Следующий() Цикл
		
		Объект.ОтпускаСотрудников[ТаблицаОтпусков.НомерСтроки - 1].ОбщаяПродолжительностьОтпуска = ТаблицаОтпусков.ОбщаяПродолжительностьОтпуска;
		
	КонецЦикла;
	
	
КонецПроцедуры


&НаСервере                                       
Функция ПоместитьДанныеВоВременноеХранилище() 
	
	Возврат ПоместитьВоВременноеХранилище(Объект.ОтпускаСотрудников.Выгрузить(), УникальныйИдентификатор);  
	
КонецФункции 

&НаСервере
Процедура ЗаполнитьСотрудникамиНаСервере() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	BKM_Сотрудники.Ссылка КАК Сотрудник
		|ИЗ
		|	Справочник.BKM_Сотрудники КАК BKM_Сотрудники
		|ГДЕ
		|	НЕ BKM_Сотрудники.ПометкаУдаления";
	
	СписокСотрудников = Запрос.Выполнить().Выгрузить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СписокСотрудников,Объект.ОтпускаСотрудников);
	
КонецПроцедуры


#КонецОбласти


