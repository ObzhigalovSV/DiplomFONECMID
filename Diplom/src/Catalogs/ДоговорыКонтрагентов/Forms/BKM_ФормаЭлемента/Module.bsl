#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДобавитьНовыеРеквизитыФормы();
	ДобавитьНовыеЭлементыФормы();  
	
КонецПроцедуры 

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи) 
	
	Если Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.BKM_АбонентскоеОбслуживание Тогда
		
		//Перенести период в элемент справочника
		ТекущийОбъект.BKM_ДатаНачалаДействияДоговора =	ЭтотОбъект.ПериодДействияДоговора.ДатаНачала;
		ТекущийОбъект.BKM_ДатаОкончанияДействияДоговора = ЭтотОбъект.ПериодДействияДоговора.ДатаОкончания;
		
	Иначе
		
		// Убрать избыточные данные      
		ПустаяДата = Дата(1,1,1);
		ТекущийОбъект.BKM_ДатаНачалаДействияДоговора = ПустаяДата;
		ТекущийОбъект.BKM_ДатаОкончанияДействияДоговора = ПустаяДата;
		ТекущийОбъект.BKM_ЕжемесячнаяАбонентскаяПлата = 0;
		ТекущийОбъект.BKM_СтоимостьЧасаРаботыСпециалиста = 0;
		
	КонецЕсли ; 
	
КонецПроцедуры

#КонецОбласти    


#Область СлужебныеПроцедурыИФункции   

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	
	УстановитьВидимостьНовыхЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры 

&НаСервере
Процедура ДобавитьНовыеРеквизитыФормы()

	//Добавить реквизит ПериодДействияДоговора (Для удобства ввода дат)
	ДобавляемыеРеквизиты = Новый Массив;
	Реквизит_ПериодДействияДоговора = Новый РеквизитФормы("ПериодДействияДоговора",	Новый ОписаниеТипов("СтандартныйПериод")); 
	ДобавляемыеРеквизиты.Добавить(Реквизит_ПериодДействияДоговора);
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);  
	
	// Заполнить период согласно дат действия договора
	ЭтотОбъект.ПериодДействияДоговора.ДатаНачала = НачалоДня(Объект.BKM_ДатаНачалаДействияДоговора);
	ЭтотОбъект.ПериодДействияДоговора.ДатаОкончания = КонецДня(Объект.BKM_ДатаОкончанияДействияДоговора);	
	
КонецПроцедуры   

&НаСервере
Процедура ДобавитьНовыеЭлементыФормы()	
		
	// Создать группу для новых элементов формы.       
	Если Элементы.Найти("ГруппаПрограммноСозданныеЭлементыФормы") = Неопределено Тогда
		ГруппаПрограммноСозданныеЭлементыФормы = Элементы.Добавить("ГруппаПрограммноСозданныеЭлементыФормы", Тип("ГруппаФормы"),ЭтотОбъект);
		ГруппаПрограммноСозданныеЭлементыФормы.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаПрограммноСозданныеЭлементыФормы.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		ГруппаПрограммноСозданныеЭлементыФормы.ОтображатьЗаголовок = Ложь;
		ГруппаПрограммноСозданныеЭлементыФормы.Отображение = ОтображениеОбычнойГруппы.Нет; 
	КонецЕсли;
	
	Если Элементы.Найти("АбонентскаяПлата") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("АбонентскаяПлата", Тип("ПолеФормы"),ГруппаПрограммноСозданныеЭлементыФормы);		
		НовыйЭлемент.ПутьКДанным = "Объект.BKM_ЕжемесячнаяАбонентскаяПлата";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок = "Ежемесячная абонентская плата"; 
	КонецЕсли;
	
	Если Элементы.Найти("СтоимостьЧасаРаботы") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("СтоимостьЧасаРаботы", Тип("ПолеФормы"), ГруппаПрограммноСозданныеЭлементыФормы);		
		НовыйЭлемент.ПутьКДанным = "Объект.BKM_СтоимостьЧасаРаботыСпециалиста";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок = "Стоимость часа работы специалиста";  
	КонецЕсли;
			
	Если Элементы.Найти("ПериодДействияДоговора") = Неопределено Тогда
		НовыйЭлемент = Элементы.Добавить("ПериодДействияДоговора", Тип("ПолеФормы"),ГруппаПрограммноСозданныеЭлементыФормы );		
		НовыйЭлемент.ПутьКДанным = "ПериодДействияДоговора";
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.Заголовок = "Период действия договора";
	КонецЕсли;
	
	Элементы.ВидДоговора.УстановитьДействие("ПриИзменении", "ВидДоговораПриИзменении");
	
	УстановитьВидимостьНовыхЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьНовыхЭлементовФормы(Форма)     
		
	Форма.Элементы.ГруппаПрограммноСозданныеЭлементыФормы.Видимость = Форма.Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.BKM_АбонентскоеОбслуживание");		
			
КонецПроцедуры

#КонецОбласти
